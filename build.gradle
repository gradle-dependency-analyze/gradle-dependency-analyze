plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'jacoco'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.12'
}

group = 'ca.cutterslade.gradle'
version = '1.9.3-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.apache.maven.shared:maven-dependency-analyzer:1.13.2') {
        exclude group: 'org.apache.maven'
    }

    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    testImplementation 'org.apache.commons:commons-text:1.12.0'
}

tasks.named('publishPlugins') {
    it.dependsOn check
}

gradlePlugin {
    website = 'https://github.com/gradle-dependency-analyze/gradle-dependency-analyze'
    vcsUrl = 'https://github.com/gradle-dependency-analyze/gradle-dependency-analyze.git'
    plugins {
        dependencyAnalyze {
            id = 'ca.cutterslade.analyze'
            displayName = 'Gradle Dependency Analyze'
            description = 'Dependency analysis plugin for gradle. This plugin attempts to replicate the functionality of the maven dependency plugin\'s analyze goals which fail the build if dependencies are declared but not used or used but not declared.'
            tags.addAll('dependency', 'verification', 'analyze')
            implementationClass = 'ca.cutterslade.gradle.analyze.AnalyzeDependenciesPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required.set true
        csv.required.set true
    }
}

jacoco {
    toolVersion = '0.8.11'
}
