plugins {
  id 'java'
  id 'groovy'
  id 'maven'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.7.1'
  id 'com.jfrog.artifactory' version '4.2.0'
}

group = 'ca.cutterslade.gradle'
version = '1.1.0'

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile 'org.apache.maven.shared:maven-dependency-analyzer:1.6'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact javadocJar {classifier 'javadoc'}
      artifact sourcesJar {classifier 'sources'}
    }
  }

  repositories {
    maven {
      url "$buildDir/repo"
    }
  }
}

if (this.hasProperty('bintrayUser') && this.hasProperty('bintrayApiKey')) bintray {
  user = bintrayUser
  key = bintrayApiKey
  publications = ['mavenJava']
  pkg {
    repo = 'maven'
    name = project.name
    desc = 'Dependency analysis plugin for gradle'
    websiteUrl = 'https://github.com/wfhartford/gradle-dependency-analyze'
    issueTrackerUrl = 'https://github.com/wfhartford/gradle-dependency-analyze/issues'
    vcsUrl = 'https://github.com/wfhartford/gradle-dependency-analyze.git'
  }
}

if (this.hasProperty('jfrogUser') && this.hasProperty('jfrogPassword')) artifactory {
  contextUrl = 'http://oss.jfrog.org/artifactory'
  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = jfrogUser
      password = jfrogPassword
      maven = true
    }
    defaults {
      publications ('mavenJava')
    }
  }
}

task javaVersionCheck() << {
  if (JavaVersion.current() != JavaVersion.VERSION_1_6) {
    throw new GradleException('Build must be run on Java 6')
  }
}

tasks.withType(PublishToMavenRepository) {
  it.dependsOn check
  it.dependsOn javaVersionCheck
}

